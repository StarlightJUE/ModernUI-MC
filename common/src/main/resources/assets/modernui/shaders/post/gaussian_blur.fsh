#version 330
#extension GL_ARB_separate_shader_objects : require

// This file is part of Modern UI.
// Copyright (C) 2024 BloCamLimb.
// Licensed under LGPL-3.0-or-later.

#include <minecraft:globals.glsl>

uniform sampler2D InSampler;

layout(std140) uniform SamplerInfo {
    vec2 OutSize;
    vec2 InSize;
};

layout(std140) uniform BlurInfo {
    vec2 BlurDir;
};

layout(location = 0) in vec2 texCoord;

layout(location = 0) out vec4 fragColor;

void main() {
    int radius = MenuBlurRadius;
    if (radius <= 0) {
        fragColor = texture(InSampler, texCoord);
        return;
    }

    vec2 oneTexel = 1.0 / InSize;
    vec4 blur = vec4(0.0);

    // sigma = radius / 2.0
    // base = -0.5 / (sigma * sigma)
    // factor = 1.0 / (sigma * sqrt(2*PI))
    float base = -2.0 / (radius * radius);
    float factor = 0.79788456 / radius;
    ivec2 bound = ivec2(InSize) - 1;
    ivec2 basePos = ivec2(texCoord * InSize);
    ivec2 blurDir = ivec2(BlurDir);
    float wsum = 0.0, w;
    for (int r = -radius; r <= radius; r += 1) {
        w = exp(r * r * base) * factor;
        blur += texelFetch(InSampler, clamp(basePos + r * blurDir, ivec2(0), bound), 0) * w;
        wsum += w;
    }

    fragColor = vec4(blur.rgb / wsum, 1.0);
}